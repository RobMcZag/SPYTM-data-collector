plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.flywaydb.flyway" version "6.2.0"
    id 'org.unbroken-dome.test-sets' version '2.2.1'
    id 'idea'   // To be able to set testDirs as testSourceSets
}

group 'com.robertozagni'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

testSets {

    libraries { testHelpers }

    unitTest {
        imports testHelpers
    }
    integrationTest {
        dirName = 'test-integration'
        imports testHelpers
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'com.h2database:h2'             // H2 in memory DB - for local running and tests
    runtimeOnly 'org.postgresql:postgresql'     // Postgress DB - for Prod and pre-prod Tests

    // testHelpersCompileOnly sourceSets.main.output
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testHelpersImplementation 'org.springframework.boot:spring-boot-starter-web'
}

tasks.withType(Test) {  // make all Test sets use JUnit 5 - 'test' would have used it for unit tests only :)
    useJUnitPlatform()
}

integrationTest {   // Runs at least onces a day
    inputs.property "integration.date", LocalDate.now()
}

integrationTest.shouldRunAfter test     // Fail fast on Unit tests, eventually.
check.dependsOn integrationTest         // To have integrationTests run during the build

flyway {    // To use Flyway targets without running Spring Boot
    url = 'jdbc:postgresql://localhost:5432/spytm_test_db'
    user = 'spytm_test_user'
    password = '1234-test-PW'
    schemas = ['spytm']
    // placeholders = ['name': 'val',]
}

idea {
    module {
        testSourceDirs += project.sourceSets.testHelpers.java.srcDirs
        testSourceDirs += project.sourceSets.testHelpers.resources.srcDirs

        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}